<mainFunction> = MAIN SQO SQC <stmtsAndFunctionDefs> END $
<stmtsAndFunctionDefs> = <stmtOrFunctionDef> <ext_stmtsAndFunctionDefs>
<ext_stmtsAndFunctionDefs> = <stmtsAndFunctionDefs>
<ext_stmtsAndFunctionDefs> = E
<stmtOrFunctionDef> = <stmt>
<stmtOrFunctionDef> = <functionDef>
<stmt> = <declarationStmt>
<stmt> = <assignmentStmt_type1>
<stmt> = <assignmentStmt_type2>
<stmt> = <ifStmt>
<stmt> = <ioStmt>
<stmt> = <funCallStmt> SEMICOLON
<functionDef> = FUNCTION SQO <parameter_list> SQC ASSIGNOP FUNID SQO <parameter_list> SQC <stmtsAndFunctionDefs> END SEMICOLON
<parameter_list> = <type> ID <remainingList>
<type> = INT
<type> = REAL
<type> = STRING
<type> = MATRIX
<remainingList> = COMMA <parameter_list>
<remainingList> = E
<declarationStmt> = <type> <var_list> SEMICOLON
<var_list> = ID <more_ids>
<more_ids> = COMMA <var_list>
<more_ids> = E
<assignmentStmt_type1> = <leftHandSide_singleVar> ASSIGNOP <rightHandSide_type1> SEMICOLON
<assignmentStmt_type2> = <leftHandSide_listVar> ASSIGNOP <rightHandSide_type2> SEMICOLON
<leftHandSide_singleVar> = ID
<leftHandSide_listVar> = SQO <var_list> SQC
<rightHandSide_type1> = <arithmeticExpression>
<rightHandSide_type1> = <sizeExpression>
<rightHandSide_type1> = <funCallStmt>
<rightHandSide_type2> = <sizeExpression>
<rightHandSide_type2> = <funCallStmt>
<sizeExpression> = SIZE ID
<ifStmt> = IF OP <booleanExpression> CL <stmt> <otherStmts> <ext_ifStmt>
<ext_ifStmt> = ELSE <stmt> <otherStmts> ENDIF SEMICOLON
<ext_ifStmt> = ENDIF SEMICOLON
<otherStmts> = <stmt> <otherStmts>
<otherStmts> = E
<ioStmt> = READ OP ID CL SEMICOLON
<ioStmt> = PRINT OP ID CL SEMICOLON
<funCallStmt> = FUNID OP <inputParameterList> CL
<inputParameterList> = <var> <listVar>
<inputParameterList> = E
<listVar> = COMMA <inputParameterList>
<listVar> = E
<arithmeticExpression> = <arithmeticTerm> <ext_arithmeticExpression>
<ext_arithmeticExpression> = <operator_lowPrecedence> <arithmeticExpression>
<ext_arithmeticExpression> = E
<arithmeticTerm> = <factor> <ext_arithmeticTerm>
<ext_arithmeticTerm> = <operator_highPrecedence> <arithmeticTerm>
<ext_arithmeticTerm> = E
<factor> = OP <arithmeticExpression> CL
<factor> = <var>
<operator_lowPrecedence> = PLUS
<operator_lowPrecedence> = MINUS
<operator_highPrecedence> = MUL
<operator_highPrecedence> = DIV
<booleanExpression> = OP <booleanExpression> CL <logicalOp> OP <booleanExpression> CL
<booleanExpression> = <constrainedVars> <relationalOp> <constrainedVars>
<constrainedVars> = ID
<constrainedVars> = NUM
<constrainedVars> = RNUM
<var> = ID <matrixElement>
<var> = NUM
<var> = RNUM
<var> = STR
<var> = <matrix>
<matrix> = SQO <rows> SQC
<rows> = <row> <ext_rows>
<ext_rows> = SEMICOLON <rows>
<ext_rows> = E
<row> = NUM <ext_row>
<ext_row> = <remainingColElements>
<ext_row> = E
<remainingColElements> = COMMA NUM <remainingColElements>
<remainingColElements> = E
<matrixElement> = SQO NUM COMMA NUM SQC
<matrixElement> = E
<logicalOp> = AND
<logicalOp> = OR
<booleanExpression> = NOT OP <booleanExpression> CL
<relationalOp> = LT
<relationalOp> = LE
<relationalOp> = EQ
<relationalOp> = GT
<relationalOp> = GE
<relationalOp> = NE
